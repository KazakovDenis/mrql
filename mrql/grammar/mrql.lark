?start: pipeline

pipeline: stage (";" stage) *";"?

stage: match_stage
     | limit_stage
     | set_stage
     | project_stage

match_stage: "MATCH" expr
limit_stage: "LIMIT" value
set_stage: "SET" NAME "=" expr
project_stage: "PROJECT" projection ("," projection)*

projection: NAME "=" expr
          | NAME               -> project_field

?expr: or_expr

?or_expr: and_expr
        | or_expr "OR" and_expr   -> or

?and_expr: cmp_expr
          | and_expr "AND" cmp_expr -> and

?cmp_expr: sum
          | sum "=" sum           -> eq
          | sum "!=" sum          -> ne
          | sum ">" sum           -> gt
          | sum ">=" sum          -> gte
          | sum "<" sum           -> lt
          | sum "<=" sum          -> lte

?sum: term
    | sum "+" term -> add
    | sum "-" term -> sub

?term: factor
     | term "*" factor -> mul
     | term "/" factor -> div

?factor: atom
       | "-" factor -> neg

?atom: NAME                          -> var
     | "@"NAME                       -> param
     | ESCAPED_STRING                -> string
     | NUMBER                        -> number
     | array
     | func_call
     | field_ref

array : "[" [expr ("," expr)*] "]"
field_ref: "$"NAME ("."NAME)*
value: NUMBER
func_call: NAME "(" [expr ("," expr)*] ")"

%import common.CNAME -> NAME
%import common.SIGNED_NUMBER -> NUMBER
%import common.ESCAPED_STRING

%import common.WS
%ignore WS

%import common.WS_INLINE
%ignore WS_INLINE

COMMENT: /--.*/
%ignore COMMENT
