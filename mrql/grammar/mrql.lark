?start: pipeline

pipeline: stage (";" stage) *";"?

stage: match_stage
     | limit_stage
     | set_stage
     | project_stage

match_stage: "MATCH" expr
limit_stage: "LIMIT" value_or_param
set_stage: "SET" CNAME "=" expr
project_stage: "PROJECT" projection ("," projection)*

projection: CNAME "=" expr
          | CNAME -> project_field

?expr: or_expr

value_or_param:
          | NUMBER -> number
          | "@" CNAME -> param

?or_expr: and_expr
          | or_expr "OR" and_expr   -> or

?and_expr: cmp_expr
          | and_expr "AND" cmp_expr -> and

?cmp_expr: sum
          | sum "=" sum           -> eq
          | sum "!=" sum          -> ne
          | sum ">" sum           -> gt
          | sum ">=" sum          -> gte
          | sum "<" sum           -> lt
          | sum "<=" sum          -> lte

?sum: term
    | sum "+" term -> add
    | sum "-" term -> sub

?term: factor
     | term "*" factor -> mul
     | term "/" factor -> div

?factor: atom
       | "-" factor -> neg

DOTTED_NAME: CNAME ("." CNAME)*

?atom: DOTTED_NAME           -> var
     | "@" CNAME             -> param
     | ESCAPED_STRING        -> string
     | NUMBER                -> number
     | array
     | func_call
     | field_ref

array: "[" [expr ("," expr)*] "]"
field_ref: "$" DOTTED_NAME
func_call: CNAME "(" [expr ("," expr)*] ")"

%import common.CNAME
%import common.SIGNED_NUMBER -> NUMBER
%import common.ESCAPED_STRING

%import common.WS
%ignore WS

%import common.WS_INLINE
%ignore WS_INLINE

COMMENT: /--.*/
%ignore COMMENT
